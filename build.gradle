plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE' apply(false)
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

ext {
    set('springCloudVersion', "Hoxton.SR1")
}

group = 'com.maigroup.smartrent'
subprojects {

//    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'groovy'
    sourceCompatibility = 1.8


    repositories {
        //TODO: remove maven local and fetch the dependencies from GitHub repository (parse maven settings.xml to get the credentials).
        mavenLocal()
        mavenCentral()
    }


    dependencies {
        implementation "com.google.guava:guava:${project['google-guava.version']}"
        implementation "com.mg.platform:persistence:${project['mg-persistence.version']}"
        implementation "com.mg.smartrent:domain-model:${project['mg-domain-model.version']}"
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-security'

        testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
            exclude module: "spring-boot-starter-logging"
            exclude module: "logback-classic"
        }
        testImplementation(group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo')
        testImplementation(group: 'org.spockframework', name: 'spock-core', version: "${project['spockframework.verson']}")
        testImplementation(group: 'org.spockframework', name: 'spock-spring', version: "${project['spockframework.verson']}")
    }

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

def manifestAttributes(String projectName, String mainClass) {
    println('> -------------------------------------------------')
    println('> PROJECT    : ' + projectName)
    println('> ENTRY-CLASS: ' + mainClass)
    return [
            'Specification-Title'     : projectName,
            'Main-Class'              : mainClass,
            'Specification-Vendor'    : 'MaiGroup',
            'Specification-Vendor-URL': 'http://www.maigroup.com',
            'Specification-Version'   : version,
            'Implementation-Title'    : group,
            'Implementation-Vendor'   : 'MaiGroup',
            'Implementation-URL'      : 'http://www.maigroup.com',
            'Implementation-Version'  : version,
            'Created-By'              : "${System.getProperty('java.version')} (${System.getProperty('java.specification.vendor')})",
            'Built-By'                : System.getProperty("user.name"),
            'Build-Date'              : (new Date()).format("Y-MM-dd H:m:s")
    ]
}